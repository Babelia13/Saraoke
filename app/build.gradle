plugins {
    id 'com.android.application'

    id 'kotlin-android'
    id "kotlin-kapt"

    id 'io.gitlab.arturbosch.detekt'
    id 'com.gladed.androidgitversion'
}

apply from: "$rootDir/gradle/properties_utils.gradle"

androidGitVersion {
    codeFormat "MNNPP"
    tagPattern(/^[0-9]+.*/) // Tag names should follow the pattern vM.NN.PP
}

ext {

    /*
     ********************
     *
     * Android variables
     *
     ********************
     */
    compile_sdk_version = 30
    min_sdk_version = 22
    target_sdk_version = 30
    build_tools_version = "30.0.3"

    // The Android version is multiplied by a big number so it is placed in the leftmost part
    // of the version code because we can't add it in androidGitVersion's codeFormat parameter
    version_code = compile_sdk_version * 100000 + androidGitVersion.code()
    version_name = androidGitVersion.name()

    /*
     *******************
     *
     * Library versions
     *
     *******************
     */
    kodein_version = "7.8.0"
    mini_version = "3.0.0"
    retrofit_version = "2.9.0"
    okhttp_version = "4.9.1"
    moshi_version = "1.12.0"
    accompanist_version = "0.11.1"

    geniusApiToken = loadPropertiesFile("settings.properties")["geniusApiToken"] ?: ""
}

android {
    compileSdk compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.babelia.saraoke"
        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField "String", "GENIUS_API_TOKEN", geniusApiToken
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        // Add kotlin folders as possible source folders
        all {
            java.srcDirs += "src/${name}/kotlin"
        }

        // Add common test folder so both androidTest and test can share common classes
        test {
            java.srcDirs += ["$projectDir/src/testShared/java", "$projectDir/src/testShared/kotlin"]
        }

        androidTest {
            java.srcDirs += ["$projectDir/src/testShared/java", "$projectDir/src/testShared/kotlin"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.10'
    }
}

dependencies {

    // Android support core and architecture libs
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"

    // Jetpack Compose and UI
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.activity:activity-compose:1.3.1"
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha06"

    // Flux architecture: Mini
    implementation "com.github.hyperdevs-team.mini-kotlin:mini-android:$mini_version"
    implementation "com.github.hyperdevs-team.mini-kotlin:mini-kodein:$mini_version"
    implementation "com.github.hyperdevs-team.mini-kotlin:mini-kodein-android:$mini_version"
    implementation "com.github.hyperdevs-team.mini-kotlin:mini-kodein-android-compose:$mini_version"
    kapt "com.github.hyperdevs-team.mini-kotlin:mini-processor:$mini_version"

    // Dependency injection: Kodein
    implementation "org.kodein.di:kodein-di-jvm:$kodein_version"
    implementation "org.kodein.di:kodein-di-framework-android-x:$kodein_version"
    implementation "org.kodein.di:kodein-di-conf-jvm:$kodein_version"
    implementation "org.kodein.di:kodein-di-framework-compose:$kodein_version"

    // Network utils
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    implementation 'org.jsoup:jsoup:1.14.2'

    // Utils
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Image utils
    implementation "com.google.accompanist:accompanist-coil:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    // Detekt formatting plugin
    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:${detekt_version}")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}

afterEvaluate {
    detekt {
        toolVersion = detekt_version
        input = files(android.sourceSets.collect { it.java.srcDirs.collect { it.path } }.flatten())
        config = files("${project.rootDir}/config/detekt/detekt.yml")
    }
}
